     /* =================================================================== */
     /*  RPGUnit - Create distribution package.                             */
     /* =================================================================== */
     /*  Updates the MKMETADATA source member and saves the                 */
     /*  RPGUNIT library to save file RPGUNIT/RPGUNIT.                      */
     /* =================================================================== */
     /*   >>PRE-COMPILER<<                                                  */
     /*     >>CRTCMD<<  CRTBNDCL      PGM(&LI/&OB) +                        */
     /*                               SRCFILE(&SL/&SF)  +                   */
     /*                               SRCMBR(&SM);                          */
     /*     >>COMPILE<<                                                     */
     /*       >>PARM<< DBGVIEW(*LIST);                                      */
     /*     >>END-COMPILE<<                                                 */
     /*     >>LINK<<                                                        */
     /*       >>PARM<< DFTACTGRP(*NO);                                      */
     /*       >>PARM<< ACTGRP(*NEW);                                        */
     /*     >>END-LINK<<                                                    */
     /*     >>EXECUTE<<                                                     */
     /*   >>END-PRE-COMPILER<<                                              */
     /* =================================================================== */
             PGM

             DCL        VAR(&LIB       ) TYPE(*CHAR) LEN(10) VALUE('RPGUNIT')
             DCL        VAR(&SAVF      ) TYPE(*CHAR) LEN(10) VALUE('RPGUNIT')
             DCL        VAR(&TGTRLS    ) TYPE(*CHAR) LEN(10) VALUE('V6R1M0')
             DCL        VAR(&TEXT      ) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RELEASE   ) TYPE(*CHAR) LEN(20)
             DCL        VAR(&TMPLIB    ) TYPE(*CHAR) LEN(10) VALUE('QTEMP')
             DCL        VAR(&OWNER     ) TYPE(*CHAR) LEN(10) VALUE('QPGMR')

             /* ---------------------------------------------------- */
             /*  Delete object that are not necessary.               */
             /* ---------------------------------------------------- */
             DLTOBJ OBJ(&LIB/*ALL) OBJTYPE(*MODULE)
             MONMSG MSGID(CPF2125) /* No object deleted */

             DLTOBJ OBJ(&LIB/*ALL) OBJTYPE(*PGM)
             MONMSG MSGID(CPF2125) /* No object deleted */

             DLTOBJ OBJ(&LIB/*ALL) OBJTYPE(*SRVPGM)
             MONMSG MSGID(CPF2125) /* No object deleted */

             DLTF   FILE(&LIB/EVFEVENT)
             MONMSG MSGID(CPF2105)

             DLTOBJ OBJ(&LIB/*ALL) OBJTYPE(*DTAARA)
             MONMSG MSGID(CPF2125) /* No object deleted */

             /* ---------------------------------------------------- */
             /*  Create member list of QSRC and get release#.        */
             /* ---------------------------------------------------- */
             CRTBNDRPG  PGM(&TMPLIB/CRTMETADTA) +
                          SRCFILE(*LIBL/QBUILD) SRCMBR(*MODULE) +
                          DBGVIEW(*LIST) TRUNCNBR(*NO)
             DLTMOD     MODULE(&TMPLIB/CRTMETADTA)

             CALL       PGM(&TMPLIB/CRTMETADTA) PARM(&LIB &RELEASE)

             /* ---------------------------------------------------- */
             /*  Re-create objects for the specified target release  */
             /* ---------------------------------------------------- */
             CRTBNDCL   PGM(&TMPLIB/MKLLIST) SRCFILE(QLLIST)
             CALL       PGM(&TMPLIB/MKLLIST) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(&TMPLIB/MKTESTCASE) SRCFILE(QSRC)
             CALL       PGM(&TMPLIB/MKTESTCASE) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(&TMPLIB/MKCRTTST) SRCFILE(QSRC)
             CALL       PGM(&TMPLIB/MKCRTTST) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(&TMPLIB/MKCALLTST) SRCFILE(QSRC)
             CALL       PGM(&TMPLIB/MKCALLTST) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(&TMPLIB/MKRMTRUN) SRCFILE(QSRC)
             CALL       PGM(&TMPLIB/MKRMTRUN) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(&TMPLIB/MKCMD) SRCFILE(QCMD)
             CALL       PGM(&TMPLIB/MKCMD) PARM(&LIB &TGTRLS)

             CRTBNDCL   PGM(&TMPLIB/CRTPKGOWN) SRCFILE(QSRC)
             CALL       PGM(&TMPLIB/CRTPKGOWN) PARM(&LIB &TMPLIB &OWNER)

             CRTBNDCL   PGM(&TMPLIB/CRTPKGDBG) SRCFILE(QSRC)
             CALL       PGM(&TMPLIB/CRTPKGDBG) PARM(&LIB &TMPLIB)

             /* ---------------------------------------------------- */
             /*  Delete object that are not necessary.               */
             /* ---------------------------------------------------- */
             DLTOBJ OBJ(&LIB/*ALL) OBJTYPE(*MODULE)
             MONMSG MSGID(CPF2125) /* No object deleted */

             /* ---------------------------------------------------- */
             /*  Save install package to save file                   */
             /* ---------------------------------------------------- */
             CHGVAR     VAR(&TEXT) +
                          VALUE('RPGUNIT Distribution Package - ' *CAT &RELEASE)
             CHGLIB     LIB(&LIB) TEXT(&TEXT)

             DLTF       FILE(&TMPLIB/&SAVF)
             MONMSG     MSGID(CPF2105)

             CRTSAVF    FILE(&TMPLIB/&SAVF) TEXT(&TEXT)

      /*        SAVLIB     LIB(&LIB) DEV(*SAVF) SAVF(&TMPLIB/&SAVF) +          */
      /*                     TGTRLS(&TGTRLS) DTACPR(*HIGH) +                   */
      /*                     OMITOBJ((&LIB/*ALL     *PGM   )  +                */
      /*                          (&LIB/*ALL     *SRVPGM)  +                   */
      /*                          (&LIB/*ALL     *MODULE)  +                   */
      /*                          (&LIB/*ALL     *CMD   )  +                   */
      /*                          (&LIB/*ALL     *DTAARA)  +                   */
      /*                          (&LIB/*ALL     *USRSPC)  +                   */
      /*                          (&LIB/*ALL     *JOBD  )  +                   */
      /*                          (&LIB/*ALL     *PNLGRP)  +                   */
      /*                          (&LIB/*ALL     *WSCST )  +                   */
      /*                          (&LIB/&SAVF    *FILE  )  +                   */
      /*                             (&LIB/EVFEVENT *FILE  ))                  */

             SAVLIB     LIB(&LIB) DEV(*SAVF) SAVF(&TMPLIB/&SAVF) +
                          TGTRLS(&TGTRLS) DTACPR(*HIGH) +
                          SELECT( +
                            (*INCLUDE &LIB/RUCALLTST  *PGM)    +
                            (*INCLUDE &LIB/RUCRTTST   *PGM)    +
                            (*INCLUDE &LIB/RUPGMRMT   *PGM)    +
                            (*INCLUDE &LIB/RUTAGTST   *PGM)    +
                            (*INCLUDE &LIB/LLIST      *SRVPGM) +
                            (*INCLUDE &LIB/RURUNNER   *SRVPGM) +
                            (*INCLUDE &LIB/RURUNRMT   *SRVPGM) +
                            (*INCLUDE &LIB/RUTESTCASE *SRVPGM) +
                            (*INCLUDE &LIB/QCMD       *FILE)   +
                            (*INCLUDE &LIB/QTESTCASES *FILE)   +
                            (*INCLUDE &LIB/QUNITTEST  *FILE)   +
                            (*INCLUDE &LIB/QINCLUDE   *FILE)   +
                            (*INCLUDE &LIB/QSRC       *FILE)   +
                            (*INCLUDE &LIB/RPGUNIT    *JOBD)   +
                            (*INCLUDE &LIB/RUCALLTST  *CMD)    +
                            (*INCLUDE &LIB/RUCRTTST   *CMD)    +
                            (*INCLUDE &LIB/RUCALLTST  *PNLGRP) +
                            (*INCLUDE &LIB/RUCRTTST   *PNLGRP) +
                            (*INCLUDE &LIB/RUWSCST    *WSCST))

             DLTF       FILE(&LIB/&SAVF)
             MONMSG     MSGID(CPF2105)

             MOVOBJ     OBJ(&TMPLIB/&SAVF) OBJTYPE(*FILE) TOLIB(&LIB)

             DLTPGM     PGM(&TMPLIB/CRTMETADTA)

             ENDPGM
